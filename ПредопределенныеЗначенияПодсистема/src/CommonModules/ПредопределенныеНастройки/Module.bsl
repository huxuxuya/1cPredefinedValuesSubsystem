#Область ПрограммныйИнтерфейс

// Значение по имени настройки.
// 
// Параметры:
//  ИмяНастройки - Строка - Имя настройки
//  ВызыватьИсключение - Булево - Вызывать исключение
// 
// Возвращаемое значение: 
//  Произвольный - Значение по имени настройки 
//  
Функция Значение(ИмяНастройки, ВызыватьИсключение = Ложь) Экспорт
	
	Возврат ПредопределенныеНастройкиПовтИсп.Значение(ИмяНастройки, ВызыватьИсключение);
	
КонецФункции

// Возвращает ссылку на предопределенную настройку по имени.
// 
// Параметры:
// 	ИмяНастройки - Строка - Имя настройки
// 	
// Возвращаемое значение:
// 	СправочникСсылка.ПредопределенныеНастройки - Ссылка на предопределенную настройку
// 	
Функция НастройкаПоИмени(ИмяНастройки) Экспорт
	
	Возврат ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(ИмяНастройки);
	
КонецФункции

Функция ЗначениеПредопределеннойНастройки(ИмяНастройки, ВызыватьИсключение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПредопределенныеНастройки.Значение КАК Значение
		|ИЗ
		|	Справочник.ПредопределенныеНастройки КАК ПредопределенныеНастройки
		|ГДЕ
		|	ПредопределенныеНастройки.Ссылка = &Ссылка
		|	И ПредопределенныеНастройки.ТипЗначения <> ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив)
		|	И ПредопределенныеНастройки.ТипЗначения <> ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений)
		|	И ПредопределенныеНастройки.ТипЗначения <> ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПредопределенныеНастройки.Хранилище
		|ИЗ
		|	Справочник.ПредопределенныеНастройки КАК ПредопределенныеНастройки
		|ГДЕ
		|	ПредопределенныеНастройки.Ссылка = &Ссылка
		|	И (ПредопределенныеНастройки.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив)
		|	ИЛИ ПредопределенныеНастройки.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений)
		| 	ИЛИ ПредопределенныеНастройки.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение))	";
	
	Запрос.УстановитьПараметр("Ссылка", ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(ИмяНастройки));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат РезультатДляОтсутствующейНастройки(ИмяНастройки, ВызыватьИсключение);
	КонецЕсли;
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗначениеНастройки = ВыборкаДетальныеЗаписи.Значение;
		Если ТипЗнч(ЗначениеНастройки) = Тип("ХранилищеЗначения") Тогда
			ЗначениеНастройки = ЗначениеНастройки.Получить();
		Иначе
			ЗначениеНастройки = ВыборкаДетальныеЗаписи.Значение;
		КонецЕсли;
		Возврат ЗначениеНастройки;
	КонецЦикла;
	
КонецФункции

// Определяет тип значения предопределенной настройки.
//
// Параметры:
//   Значение - <Тип> - Значение предопределенной настройки, для которого необходимо определить тип.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ТипыЗначенияПредопределенныхЗначений - Тип значения предопределенной настройки.
Функция ТипЗначенияПредопределеннойНастройки(Значение) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка");
	ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево");
	ИначеЕсли ТипЗнч(Значение) = ("Массив") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив");
	ИначеЕсли ТипЗнч(Значение) = ("Соответствие") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение)) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка");
	ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение)) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка");
	ИначеЕсли ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		
		Если Значение.Колонки.Найти("Ключ") = Неопределено Или Значение.Колонки.Найти("Значение") = Неопределено Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений");
		Иначе
			Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");
		КонецЕсли;
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Дата");
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Функция возвращает таблицу значений с предопределенными настройками.
// Таблица содержит следующие колонки:
//		Идентификатор: Строка - уникальный идентификатор настройки.
//		ИмяНастройки: Строка - наименование настройки.
//		Хранилище: ValueStorage - хранилище значений.
//		Содержание: Строка - содержание настройки.
//		Описание - Строка - описание настройки.
//		ТипЗначения: CatalogRef.ПредопределенныеНастройки - ссылка на тип значения.
//		Значение: значение параметра.
//
// Возвращаемое значение:
// ТаблицаЗначений - таблица со следующими колонками:
//		Идентификатор - УникальныйИдентификатор - уникальный идентификатор настройки.
//		ИмяНастройки - Строка - имя настройки.
//		Хранилище - ValueStorage - хранилище значений.
//		Содержание - Строка - содержание настройки.
//		Описание - Строка - описание настройки.
//		ТипЗначения - CatalogRef.ПредопределенныеНастройки - ссылка на тип значения.
//		Значение - значение параметра настройки.
Функция НоваяТаблицаПредопределенныхНастроек() Экспорт

    ТаблицаНастроек = Новый ТаблицаЗначений();
    ТаблицаНастроек.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
    ТаблицаНастроек.Колонки.Добавить("ИмяНастройки", Новый ОписаниеТипов("Строка"));
    ТаблицаНастроек.Колонки.Добавить("Хранилище", Новый ОписаниеТипов("ValueStorage"));
    ТаблицаНастроек.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
    ТаблицаНастроек.Колонки.Добавить("Описание", Новый ОписаниеТипов("Строка"));
    ТаблицаНастроек.Колонки.Добавить("ТипЗначения", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЗначенияПредопределенныхЗначений"));
    ТаблицаНастроек.Колонки.Добавить("Значение");
    
    Возврат ТаблицаНастроек;
    
КонецФункции

Процедура СоздатьОтсутствующиеПредопределенныеНастройки(ТаблицаОписанияПредопределенныхНастроек = Неопределено) Экспорт
	
	Если ТаблицаОписанияПредопределенныхНастроек = Неопределено Тогда 
		ИнициализироватьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек);
	КонецЕсли;
	
	ТаблицаОтсутствующихНастроекВБазе = ОтсутствующиеНастройкиВБазе(ТаблицаОписанияПредопределенныхНастроек);
	
	ОбновитьНастройкиВБазе(ТаблицаОтсутствующихНастроекВБазе);
	
КонецПроцедуры

// Процедура обновления настроек в базе данных.
// Параметры:
//   ТаблицаНастроекВБазе - ТаблицаЗначений - содержащая данные для обновления настроек. Обязательный параметр.
//		Идентификатор: Строка - уникальный идентификатор настройки.
//		ИмяНастройки: Строка - наименование настройки.
//		Хранилище: ValueStorage - хранилище значений.
//		Наименование: Строка - содержание настройки.
//		Описание - Строка - описание настройки.
//		ТипЗначения: Перечисление.ТипыЗначенияПредопределенныхЗначений - ссылка на тип значения.
//		Значение: значение параметра.
Процедура ОбновитьНастройкиВБазе(ТаблицаНастроекВБазе) Экспорт
	
	Для Каждого СтрокаНастройки Из ТаблицаНастроекВБазе Цикл
		
		НайденныйЭлемент = Справочники.ПредопределенныеНастройки.ПолучитьСсылку(СтрокаНастройки.Идентификатор);
		
		Если НайденныйЭлемент = Справочники.ПредопределенныеНастройки.ПустаяСсылка() Тогда
			НайденныйЭлемент = Справочники.ПредопределенныеНастройки.НайтиПоРеквизиту("ИмяНастройки", СтрокаНастройки.ИмяНастройки);
		КонецЕсли;
		
		Если НайденныйЭлемент <> Справочники.ПредопределенныеНастройки.ПустаяСсылка()
			И БазоваяФункциональность.СсылкаСуществует(НайденныйЭлемент) Тогда
			Элемент = НайденныйЭлемент.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ПредопределенныеНастройки.СоздатьЭлемент();
			СсылкаНового = Справочники.ПредопределенныеНастройки.ПолучитьСсылку(СтрокаНастройки.Идентификатор);
			Элемент.УстановитьСсылкуНового(СсылкаНового);
		КонецЕсли;
		
		Элемент.ТипЗначения = СтрокаНастройки.ТипЗначения;
		Элемент.ИмяНастройки = СтрокаНастройки.ИмяНастройки;
		Элемент.Наименование = СтрокаНастройки.Наименование;
		Элемент.Значение = СтрокаНастройки.Значение;
		Элемент.Описание = СтрокаНастройки.Описание;
		Элемент.Хранилище = СтрокаНастройки.Хранилище;

		Элемент.Записать();
		
	КонецЦикла;

КонецПроцедуры

Функция ОтсутствующиеНастройкиВБазе(ТаблицаОписанияПредопределенныхНастроек) Экспорт
	
	ТаблицаНовыхНастроек = НоваяТаблицаПредопределенныхНастроек();
	
	Для Каждого СтрокаНастройки Из ТаблицаОписанияПредопределенныхНастроек Цикл
		
        Элемент = Справочники.ПредопределенныеНастройки.ПолучитьСсылку(СтрокаНастройки.Идентификатор);
        Если Не БазоваяФункциональность.СсылкаСуществует(Элемент) Тогда
        	Элемент = Справочники.ПредопределенныеНастройки.НайтиПоРеквизиту("ИмяНастройки", СтрокаНастройки.ИмяНастройки);
        КонецЕсли;

        Если ЗначениеЗаполнено(Элемент) = Ложь Тогда
        	НоваяНастройка = ТаблицаНовыхНастроек.Добавить();
        	ЗаполнитьЗначенияСвойств(НоваяНастройка, СтрокаНастройки);
        КонецЕсли;
        
	КонецЦикла;
	 
	Возврат ТаблицаНовыхНастроек;
	
КонецФункции

Процедура ИнициализироватьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек) Экспорт
	
	ТаблицаОписанияПредопределенныхНастроек = НоваяТаблицаПредопределенныхНастроек();
	ЗаполнитьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек);
	ПредопределенныеНастройкиПереопределяемый.ПриЗаполненииТаблицыПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек);
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек) Экспорт

	
КонецПроцедуры



// Функция возвращает ссылку на предопределенную настройку по имени. 
// 
// Параметры:
// 	ИмяНастройки - Строка - Имя настройки
// 	ВызыватьИсключение - Булево - Если Ложь, то в случае отсутствия настройки с именем ИмяНастройки
// 									возвращается пустая ссылка. 
// Возвращаемое значение: 
// 	СправочникСсылка.ПредопределенныеНастройки - Ссылка на пустую предопределенную настройку если необходимо скрыть исключение.
//
Функция РезультатДляОтсутствующейНастройки(ИмяНастройки, ВызыватьИсключение) Экспорт
		
			Если ВызыватьИсключение Тогда
				Сообщение = БазоваяФункциональностьКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru ='Настройка с именем ""%1"" не найдена.'"),
					ИмяНастройки);
				ВызватьИсключение Сообщение;
			Иначе
				Возврат ПредопределенноеЗначение("Справочник.ПредопределенныеНастройки.ПустаяСсылка");
			КонецЕсли;
		
КонецФункции

#КонецОбласти