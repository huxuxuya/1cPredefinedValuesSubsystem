#Область ПрограммныйИнтерфейс

// Возвращает значение предопределенной настройки по ее имени.
//
// Параметры:
//  ИмяНастройки  - Строка - Имя настройки.
//  ВызыватьИсключение - Булево - Флаг, указывающий, нужно ли вызывать исключение, если настройка не найдена.
//
// Возвращаемое значение:
//  Произвольный - Значение настройки или Справочник.ПредопределенныеНастройки.ПустаяСсылка, если настройка не найдена и ВызыватьИсключение = Ложь.
//  
Функция Значение(ИмяНастройки, ВызыватьИсключение = Ложь) Экспорт
		
	Возврат ПредопределенныеНастройкиПовтИсп.Значение(ИмяНастройки, ВызыватьИсключение);
	
КонецФункции

// Возвращает ссылку на предопределенную настройку по имени.
// 
// Параметры:
// 	ИмяНастройки - Строка - Имя настройки
// 	
// Возвращаемое значение:
// 	СправочникСсылка.ПредопределенныеНастройки - Ссылка на предопределенную настройку
// 	
Функция НастройкаПоИмени(ИмяНастройки) Экспорт
	
	Возврат ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(ИмяНастройки);
	
КонецФункции

// Инициализирует таблицу предопределенных настроек.
//
// Параметры:
//   ТаблицаОписанияПредопределенныхНастроек - ТаблицаЗначений - Таблица для заполнения предопределенными настройками
Процедура ИнициализироватьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек) Экспорт
	
	ТаблицаОписанияПредопределенныхНастроек = НоваяТаблицаПредопределенныхНастроек();
	ЗаполнитьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек);
	ПредопределенныеНастройкиПереопределяемый.ПриЗаполненииТаблицыПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек);
	
КонецПроцедуры

// Возвращает новую таблицу значений с предопределенными настройками.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица со следующими колонками:
//     Идентификатор - УникальныйИдентификатор - уникальный идентификатор настройки.
//     ИмяНастройки - Строка - имя настройки.
//     Хранилище - ValueStorage - хранилище значений.
//     Наименование - Строка - содержание настройки.
//     Описание - Строка - описание настройки.
//     ТипЗначения - ПеречислениеСсылка.ТипыЗначенияПредопределенныхЗначений - ссылка на тип значения.
//     Значение - Произваольнй - значение параметра настройки.
Функция НоваяТаблицаПредопределенныхНастроек() Экспорт

    ТаблицаНастроек = Новый ТаблицаЗначений();
    ТаблицаНастроек.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
    ТаблицаНастроек.Колонки.Добавить("ИмяНастройки", Новый ОписаниеТипов("Строка"));
    ТаблицаНастроек.Колонки.Добавить("Хранилище", Новый ОписаниеТипов("ValueStorage"));
    ТаблицаНастроек.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
    ТаблицаНастроек.Колонки.Добавить("Описание", Новый ОписаниеТипов("Строка"));
    ТаблицаНастроек.Колонки.Добавить("ТипЗначения", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЗначенияПредопределенныхЗначений"));
    ТаблицаНастроек.Колонки.Добавить("Значение");
    
    Возврат ТаблицаНастроек;
    
КонецФункции

// Процедура обновления настроек в базе данных.
// Параметры:
//   ТаблицаНастроекВБазе - ТаблицаЗначений - содержащая данные для обновления настроек. Обязательный параметр.
//		Идентификатор: Строка - уникальный идентификатор настройки.
//		ИмяНастройки: Строка - наименование настройки.
//		Хранилище: ValueStorage - хранилище значений.
//		Наименование: Строка - содержание настройки.
//		Описание - Строка - описание настройки.
//		ТипЗначения: Перечисление.ТипыЗначенияПредопределенныхЗначений - ссылка на тип значения.
//		Значение: значение параметра.
Процедура ОбновитьНастройкиВБазе(ТаблицаНастроекВБазе) Экспорт
	
	Для Каждого СтрокаНастройки Из ТаблицаНастроекВБазе Цикл
		
		НайденныйЭлемент = Справочники.ПредопределенныеНастройки.ПолучитьСсылку(СтрокаНастройки.Идентификатор);
		
		Если НайденныйЭлемент = Справочники.ПредопределенныеНастройки.ПустаяСсылка() Тогда
			НайденныйЭлемент = Справочники.ПредопределенныеНастройки.НайтиПоРеквизиту("ИмяНастройки", СтрокаНастройки.ИмяНастройки);
		КонецЕсли;
		
		Если НайденныйЭлемент <> Справочники.ПредопределенныеНастройки.ПустаяСсылка()
			И СсылкаСуществует(НайденныйЭлемент) Тогда
			Элемент = НайденныйЭлемент.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ПредопределенныеНастройки.СоздатьЭлемент();
			СсылкаНового = Справочники.ПредопределенныеНастройки.ПолучитьСсылку(СтрокаНастройки.Идентификатор);
			Элемент.УстановитьСсылкуНового(СсылкаНового);
		КонецЕсли;
		

		//НоваяНастройка.Хранилище = Неопределено;
		
		
		Элемент.ТипЗначения = СтрокаНастройки.ТипЗначения;
		Элемент.ИмяНастройки = СтрокаНастройки.ИмяНастройки;
		Элемент.Наименование = СтрокаНастройки.Наименование;
		Элемент.Значение = СтрокаНастройки.Значение;
		Элемент.Описание = СтрокаНастройки.Описание;
		Элемент.Хранилище = СтрокаНастройки.Хранилище;
		
		Элемент.ТипЗначения = ТипЗначенияПредопределеннойНастройки (СтрокаНастройки.Значение);
		Если Элемент.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение")
			ИЛИ Элемент.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений") 
			ИЛИ Элемент.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив") Тогда

			Элемент.Хранилище = Новый ХранилищеЗначения(СтрокаНастройки.Значение);
		КонецЕсли;

		Элемент.Записать();
		
	КонецЦикла;

КонецПроцедуры

// Создает отсутствующие предопределенные настройки в базе данных.
//
// Параметры:
//   ТаблицаОписанияПредопределенныхНастроек - ТаблицаЗначений, Неопределено - таблица с описанием предопределенных настроек
Процедура СоздатьОтсутствующиеПредопределенныеНастройки(ТаблицаОписанияПредопределенныхНастроек = Неопределено) Экспорт
	
	Если ТаблицаОписанияПредопределенныхНастроек = Неопределено Тогда 
		ИнициализироватьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек);
	КонецЕсли;
	
	ТаблицаОтсутствующихНастроекВБазе = ОтсутствующиеНастройкиВБазе(ТаблицаОписанияПредопределенныхНастроек);
	
	ОбновитьНастройкиВБазе(ТаблицаОтсутствующихНастроекВБазе);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет тип значения предопределенной настройки.
//
// Параметры:
//   Значение - Произвольный - Значение предопределенной настройки, для которого необходимо определить тип.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ТипыЗначенияПредопределенныхЗначений - Тип значения предопределенной настройки.
Функция ТипЗначенияПредопределеннойНастройки(Значение) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка");
	ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево");
	ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив");
	ИначеЕсли ТипЗнч(Значение) = Тип("Соответствие") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение)) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка");
	ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение)) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка");
	ИначеЕсли ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		
		Если Значение.Колонки.Найти("Ключ") = Неопределено Или Значение.Колонки.Найти("Значение") = Неопределено Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений");
		Иначе
			Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");
		КонецЕсли;
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Дата");
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает значение предопределенной настройки по ее имени.
//
// Параметры:
//   ИмяНастройки - Строка - имя предопределенной настройки
//   ВызыватьИсключение - Булево - признак вызова исключения при отсутствии настройки
//
// Возвращаемое значение:
//   Произвольный- значение предопределенной настройки или Неопределено
Функция ЗначениеПредопределеннойНастройкиПоИмени(ИмяНастройки, ВызыватьИсключение) Экспорт
	
	НастройкаСсылка = ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(ИмяНастройки);
	Возврат ЗначениеПредопределеннойНастройки(НастройкаСсылка, ВызыватьИсключение);
	
КонецФункции

// Возвращает значение предопределенной настройки по ссылке на нее.
//
// Параметры:
//   НастройкаСсылка - СправочникСсылка.ПредопределенныеНастройки - Ссылка на предопределенную настройку
//   ВызыватьИсключение - Булево - Флаг, указывающий, вызывать ли исключение, если настройка отсутствует
//
// Возвращаемое значение:
//   Произвольный - Значение предопределенной настройки
Функция ЗначениеПредопределеннойНастройки(НастройкаСсылка, ВызыватьИсключение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПредопределенныеНастройки.Значение КАК Значение
		|ИЗ
		|	Справочник.ПредопределенныеНастройки КАК ПредопределенныеНастройки
		|ГДЕ
		|	ПредопределенныеНастройки.Ссылка = &Ссылка
		|	И ПредопределенныеНастройки.ТипЗначения <> ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив)
		|	И ПредопределенныеНастройки.ТипЗначения <> ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений)
		|	И ПредопределенныеНастройки.ТипЗначения <> ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПредопределенныеНастройки.Хранилище
		|ИЗ
		|	Справочник.ПредопределенныеНастройки КАК ПредопределенныеНастройки
		|ГДЕ
		|	ПредопределенныеНастройки.Ссылка = &Ссылка
		|	И (ПредопределенныеНастройки.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив)
		|	ИЛИ ПредопределенныеНастройки.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений)
		| 	ИЛИ ПредопределенныеНастройки.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение))	";
	
	Запрос.УстановитьПараметр("Ссылка", НастройкаСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ИмяНастройки = НастройкаСсылка.ИмяНастройки;
		Возврат РезультатДляОтсутствующейНастройки(ИмяНастройки, ВызыватьИсключение);
	КонецЕсли;
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗначениеНастройки = ВыборкаДетальныеЗаписи.Значение;
		Если ТипЗнч(ЗначениеНастройки) = Тип("ХранилищеЗначения") Тогда
			ЗначениеНастройки = ЗначениеНастройки.Получить();
		Иначе
			ЗначениеНастройки = ВыборкаДетальныеЗаписи.Значение;
		КонецЕсли;
		Возврат ЗначениеНастройки;
	КонецЦикла;
	
КонецФункции

// Возвращает результат для отсутствующей настройки.
//
// Параметры:
//   ИмяНастройки - Строка - имя отсутствующей настройки
//   ВызыватьИсключение - Булево - признак вызова исключения
//
// Возвращаемое значение:
//   СправочникСсылка.ПредопределенныеНастройки - ссылка на пустую настройку или исключение, если ВызыватьИсключение = Истина
Функция РезультатДляОтсутствующейНастройки(ИмяНастройки, ВызыватьИсключение) Экспорт
		
			Если ВызыватьИсключение Тогда
				Сообщение = СтрШаблон(
					НСтр("ru ='Настройка с именем ""%1"" не найдена.'"),
					ИмяНастройки);
				ВызватьИсключение Сообщение;
			Иначе
				Возврат ПредопределенноеЗначение("Справочник.ПредопределенныеНастройки.ПустаяСсылка");
			КонецЕсли;
		
КонецФункции

// Заполняет таблицу описаний предопределенных настроек.
//
// Параметры:
//   ТаблицаОписанияПредопределенныхНастроек - ТаблицаЗначений - Таблица для заполнения описания предопределенных настроек
Процедура ЗаполнитьТаблицуПредопределенныхНастроек(ТаблицаОписанияПредопределенныхНастроек) Экспорт

КонецПроцедуры

// Возвращает таблицу предопределенных настроек, отсутствующих в базе данных.
//
// Параметры:
//   ТаблицаОписанияПредопределенныхНастроек - ТаблицаЗначений - таблица с описанием предопределенных настроек
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица отсутствующих настроек в базе данных
Функция ОтсутствующиеНастройкиВБазе(ТаблицаОписанияПредопределенныхНастроек) Экспорт
	
	ТаблицаНовыхНастроек = НоваяТаблицаПредопределенныхНастроек();
	
	Для Каждого СтрокаНастройки Из ТаблицаОписанияПредопределенныхНастроек Цикл
		
        Элемент = Справочники.ПредопределенныеНастройки.ПолучитьСсылку(СтрокаНастройки.Идентификатор);
        Если Не СсылкаСуществует(Элемент) Тогда
        	Элемент = Справочники.ПредопределенныеНастройки.НайтиПоРеквизиту("ИмяНастройки", СтрокаНастройки.ИмяНастройки);
        КонецЕсли;

        Если ЗначениеЗаполнено(Элемент) = Ложь Тогда
        	НоваяНастройка = ТаблицаНовыхНастроек.Добавить();
        	ЗаполнитьЗначенияСвойств(НоваяНастройка, СтрокаНастройки);
        КонецЕсли;
        
	КонецЦикла;
	 
	Возврат ТаблицаНовыхНастроек;
	
КонецФункции

// Проверяет физическое наличие записи в информационной базе данных о переданном значении ссылки.
//
// Параметры:
//  ПроверяемаяСсылка - ЛюбаяСсылка - значение любой ссылки информационной базы данных.
// 
// Возвращаемое значение:
//  Булево - Истина, если существует.
//
Функция СсылкаСуществует(ПроверяемаяСсылка) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	[ИмяТаблицы]
	|ГДЕ
	|	Ссылка = &Ссылка
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ИмяТаблицы]", ПроверяемаяСсылка.Метаданные().ПолноеИмя());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ПроверяемаяСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти