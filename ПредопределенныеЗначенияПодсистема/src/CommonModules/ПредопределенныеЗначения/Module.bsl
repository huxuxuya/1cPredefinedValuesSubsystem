Функция Значение(ИмяНастройки, ВызыватьИсключение = Ложь) Экспорт
	
	Возврат ПредопределенныеЗначенияПовтИсп.Значение(ИмяНастройки, ВызыватьИсключение);
	
КонецФункции

Функция РезультатЗначениеИзЗапроса(ПараметрНастройка)
	
	ЭтоПредопределенный = ТипЗнч(ПараметрНастройка) = Тип("Строка");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	моноСлужебныеНастройки.Параметр КАК Параметр,
	|	моноСлужебныеНастройки.МногострочныйПараметр КАК МногострочныйПараметр,
	|	моноСлужебныеНастройки.СоставнойПараметр КАК СоставнойПараметр,
	|	моноСлужебныеНастройки.Хранилище КАК Хранилище
	|ИЗ
	|	Справочник.моноСлужебныеНастройки КАК моноСлужебныеНастройки
	|ГДЕ
	|	моноСлужебныеНастройки.Ссылка = &ПараметрНастройка
	|	И моноСлужебныеНастройки.МногострочныйПараметр = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	моноСлужебныеНастройки.Значение,
	|	моноСлужебныеНастройки.Ссылка.МногострочныйПараметр,
	|	NULL,
	|	NULL
	|ИЗ
	|	Справочник.моноСлужебныеНастройки.Параметры КАК моноСлужебныеНастройки
	|ГДЕ
	|	моноСлужебныеНастройки.Ссылка = &ПараметрНастройка
	|	И моноСлужебныеНастройки.Ссылка.МногострочныйПараметр = ИСТИНА";
	
	Если ЭтоПредопределенный Тогда
		Ссылка = моноСлужебныеНастройки.СсылкаПредопределенногоЭлемента(ПараметрНастройка);
	Иначе
		Ссылка = ПараметрНастройка;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПараметрНастройка", Ссылка);
	
	Попытка
		
		РезультатЗначение = Запрос.Выполнить();
		
	Исключение
		
		Если ЭтоПредопределенный Тогда
			
			Сообщение = моноБазоваяФункциональностьКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru ='Передано отсутствующее наименование предопределенной настройки ""%1""'"),
				ПараметрНастройка);
			ВызватьИсключение Сообщение;
				
		Иначе
			
			Сообщение = моноБазоваяФункциональностьКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru ='Передан некорректный параметр настройки. Тип: ""%1""'"),
				ТипЗнч(ПараметрНастройка));
			ВызватьИсключение Сообщение;
			
		КонецЕсли;
			
	КонецПопытки;
	
	Возврат РезультатЗначение;
	
КонецФункции