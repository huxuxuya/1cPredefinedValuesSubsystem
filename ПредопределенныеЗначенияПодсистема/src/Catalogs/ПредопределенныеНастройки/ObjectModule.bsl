#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;

	Если ТипЗначения = Перечисления.ТипыЗначенияПредопределенныхЗначений.Массив Тогда
		
		МассивЗначение = Хранилище.Получить();
		Если ТипЗнч(МассивЗначение) = Тип("Массив") Тогда
			ПредставлениеМассива = СтрСоединить(МассивЗначение, ", ");
			ПредставлениеЗначения = СтрШаблон(НСтр("ru = '[%1]'"), ПредставлениеМассива);
		КонецЕсли;
		
	ИначеЕсли ТипЗначения = Перечисления.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений Тогда
		
		ТаблицаЗначенийНастройки =  Хранилище.Получить();
		Если ТипЗнч(ТаблицаЗначенийНастройки) = Тип("ТаблицаЗначений") Тогда			
			МассивИменКолонок = Новый Массив;
			Для Каждого Колонка Из ТаблицаЗначенийНастройки.Колонки Цикл
				МассивИменКолонок.Добавить(Колонка.Имя);
			КонецЦикла;
			
			МассивПредставленийСтрок = Новый Массив;
			
			ПредставлениеДанныхТаблицы = "";
			Для Каждого Строка Из ТаблицаЗначенийНастройки Цикл
				ПредставлениеСтроки = "";
				Для Каждого Колонка Из ТаблицаЗначенийНастройки.Колонки Цикл
					
					ЗначениеЯчейки = Строка[Колонка.Имя];
					Если ПустаяСтрока(ПредставлениеСтроки) Тогда
						ПредставлениеСтроки = ЗначениеЯчейки;
					Иначе
						ПредставлениеСтроки = СтрШаблон("%1, %2", ПредставлениеСтроки, ЗначениеЯчейки);
					КонецЕсли;
				
				КонецЦикла;
				ПредставлениеСтроки = СтрШаблон("[%1]", ПредставлениеСтроки);
				МассивПредставленийСтрок.Добавить(ПредставлениеСтроки);
			КонецЦикла;
			
			ПредставлениеТаблицы = СтрСоединить(МассивПредставленийСтрок, ", ");
			ПредставлениеЗначения = СтрШаблон(НСтр("ru = '[%1]'"), ПредставлениеТаблицы);
		КонецЕсли;
		
	ИначеЕсли ТипЗначения = Перечисления.ТипыЗначенияПредопределенныхЗначений.КлючЗначение Тогда
		
		ТаблицаЗначенийНастройки =  Хранилище.Получить();
		Если ТипЗнч(ТаблицаЗначенийНастройки) = Тип("ТаблицаЗначений") Тогда
			ПредставлениеТаблицы = СтрСоединить(ТаблицаЗначенийНастройки.ВыгрузитьКолонку("Ключ"), ", ");
			ПредставлениеЗначения = СтрШаблон(НСтр("ru = 'Ключ и значение[%1]'"), ПредставлениеТаблицы);
		ИначеЕсли ТипЗнч(ТаблицаЗначенийНастройки) = Тип("Соответствие") Тогда
			МассивПредставлений = Новый Массив;
			Для Каждого КлючЗначение Из ТаблицаЗначенийНастройки Цикл 
				
				КлючЗначениеПредставление = СтрШаблон(НСтр("ru = '[%1: %2]'"), КлючЗначение.Ключ, КлючЗначение.Значение);
				МассивПредставлений.Добавить(КлючЗначениеПредставление);
				
			КонецЦикла;
			ПредставлениеЗначения = СтрСоединить(МассивПредставлений, ", ");
		КонецЕсли;
		
	ИначеЕсли ЭтоГруппа Тогда 
		ПредставлениеЗначения = СокрЛП(Наименование);
	Иначе
		ПредставлениеЗначения = СокрЛП(Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

