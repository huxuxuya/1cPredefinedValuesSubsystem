#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СозданиеПредопределенныхНастроек(Команда)
	СозданиеПредопределенныхНастроекНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТестовые(Команда)
	УстановитьТестовыеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределенныхНастроек(Команда)
	ОбновитьПовторноИспользуемыеЗначения();
	УдалениеПредопределенныхНастроекНаСервере();
	ОбновитьПовторноИспользуемыеЗначения();
	ПроверитьУдаленныеНастройки();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределеннойНастройкиСтрока(Команда)
	УдалениеПредопределеннойНастройкиСтрокаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПредопределеннойНастройкиСтрока(Команда)
	УстановитьЗначениеПредопределеннойНастройкиСтрокаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПредопределеннойНастройкиСтрока(Команда)
	СозданиеПредопределеннойНастройкиСтрокаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПредопределеннойНастройкиБулево(Команда)
	СозданиеПредопределеннойНастройкиБулевоНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПредопределеннойНастройкиБулево(Команда)
	УстановитьЗначениеПредопределеннойНастройкиБулевоНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределеннойНастройкиБулево(Команда)
	УдалениеПредопределеннойНастройкиБулевоНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПредопределеннойНастройкиСсылка(Команда)
	СозданиеПредопределеннойНастройкиСсылкаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПредопределеннойНастройкиСсылка(Команда)
	УстановитьЗначениеПредопределеннойНастройкиСсылкаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределеннойНастройкиСсылка(Команда)
	УдалениеПредопределеннойНастройкиСсылкаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПредопределеннойНастройкиМассив(Команда)
	СозданиеПредопределеннойНастройкиМассивНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПредопределеннойНастройкиМассив(Команда)
	УстановитьЗначениеПредопределеннойНастройкиМассивНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределеннойНастройкиМассив(Команда)
	УдалениеПредопределеннойНастройкиМассивНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПредопределеннойНастройкиТаблицаЗначений(Команда)
	СозданиеПредопределеннойНастройкиТаблицаЗначенийНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПредопределеннойНастройкиТаблицаЗначений(Команда)
	УстановитьЗначениеПредопределеннойНастройкиТаблицаЗначенийНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределеннойНастройкиТаблицаЗначений(Команда)
	УдалениеПредопределеннойНастройкиТаблицаЗначенийНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПредопределеннойНастройкиКлючЗначение(Команда)
	СозданиеПредопределеннойНастройкиКлючЗначениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПредопределеннойНастройкиКлючЗначение(Команда)
	УстановитьЗначениеПредопределеннойНастройкиКлючЗначениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалениеПредопределеннойНастройкиКлючЗначение(Команда)
	УдалениеПредопределеннойНастройкиКлючЗначениеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет создание предопределенных настроек на сервере.
&НаСервере
Процедура СозданиеПредопределенныхНастроекНаСервере()

	ОбновитьПовторноИспользуемыеЗначения();
	
	ТаблицаНастроек = ТаблицаПредопределенныхТестовыхНастроек();
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);
	ПроверитьСозданныеНастройки(ТаблицаНастроек);

КонецПроцедуры

&НаСервере
Процедура УстановитьТестовыеНаСервере()
	
	ТаблицаНастроек = ТаблицаПредопределенныхТестовыхНастроек();

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовоеБулево(ТаблицаНастроек);
	ИзменитьТестовоеБулево(ТаблицаНастроек);
	
	ДобавитьТестовуюСтроку(ТаблицаНастроек);
	ИзменитьТестовуюСтроку(ТаблицаНастроек);
	
	ДобавитьТестовуюСсылку(ТаблицаНастроек);
	ИзменитьТестовуюСсылку(ТаблицаНастроек);
	
	ДобавитьТестовыйМассив(ТаблицаНастроек);
	ИзменитьТестовыйМассив(ТаблицаНастроек);
	
	
	ДобавитьТестовыйКлючЗначение(ТаблицаНастроек);
	ДобавитьТестовуюТаблицуЗначений(ТаблицаНастроек);
	
	

	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);
	
	ПроверитьУстановленныеНастройки(ТаблицаНастроек);

КонецПроцедуры

// Функция возвращает таблицу предопределенных тестовых настроек.
// Возвращаемое значение:
// ТаблицаЗначений - ТаблицаЗначений, содержащая информацию о предопределенных тестовых настройках.
&НаСервере
Функция ТаблицаПредопределенныхТестовыхНастроек()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();

	ДобавитьТестовуюСтроку(ТаблицаНастроек);
	ДобавитьТестовоеБулево(ТаблицаНастроек);
	ДобавитьТестовуюСсылку(ТаблицаНастроек);
	ДобавитьТестовыйМассив(ТаблицаНастроек);
	ДобавитьТестовыйКлючЗначение(ТаблицаНастроек);
	ДобавитьТестовуюТаблицуЗначений(ТаблицаНастроек);

	Возврат ТаблицаНастроек;

КонецФункции

// Процедура проверяет, были ли созданы предопределенные настройки.
&НаСервере
Процедура ПроверитьСозданныеНастройки(ТаблицаНастроек)
	
	НастройкиНайдены = Истина;
	ИменаКнопокПоТипуНастроек = ИменаКнопокСозданияПоТипамЗначений();
	
	Для Каждого Строка Из ТаблицаНастроек Цикл
		ПроверитьЗначениеНастройки(Строка.ИмяНастройки, Строка.Значение, ИменаКнопокПоТипуНастроек);
		
		Настройка = ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(Строка.ИмяНастройки);
		Если Настройка = ПредопределенноеЗначение("Справочник.ПредопределенныеНастройки.ПустаяСсылка") Тогда
			НастройкиНайдены = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если НастройкиНайдены Тогда
		Элементы.НастройкиСозданы.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
		Элементы.НастройкиСозданы.Подсказка = НСтр("ru ='Тестовые настройки созданы'");
	Иначе
		Элементы.НастройкиСозданы.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
		Элементы.НастройкиСозданы.Подсказка = НСтр("ru ='Тестовые настройки не найдены'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьУстановленныеНастройки(ТаблицаНастроек)
	
	НастройкиУстановлены = Истина;
	ИменаКнопокПоТипуНастроек = ИменаКнопокУстановкиПоТипамЗначений();
	
	Для Каждого Строка Из ТаблицаНастроек Цикл
		ПроверитьЗначениеНастройки(Строка.ИмяНастройки, Строка.Значение, ИменаКнопокПоТипуНастроек);
		
		Настройка = ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(Строка.ИмяНастройки);
		Если Настройка = ПредопределенноеЗначение("Справочник.ПредопределенныеНастройки.ПустаяСсылка") Тогда
			НастройкиУстановлены = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если НастройкиУстановлены Тогда
		Элементы.НастройкиУстановлены.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
		Элементы.НастройкиУстановлены.Подсказка = НСтр("ru ='Тестовые настройки созданы'");
	Иначе
		Элементы.НастройкиУстановлены.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
		Элементы.НастройкиУстановлены.Подсказка = НСтр("ru ='Тестовые настройки не найдены'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗначениеНастройкиВерно(ИмяНастройки, ЗначениеИсходное)
	
	ТипЗначения = ПредопределенныеНастройки.ТипЗначенияПредопределеннойНастройки(ЗначениеИсходное);
	Возврат ЗначениеНастройкиСоответствуетЗначению(ИмяНастройки, ТипЗначения, ЗначениеИсходное);
	
КонецФункции

&НаСервере
Процедура УстановитьОформлениеКнопки(ИмяКнопки, ЗначенияИдентичны)
	
	Кнопка = Элементы[ИмяКнопки];
	Команда = Команды[ИмяКнопки];
	
	Кнопка.Картинка = БиблиотекаКартинок.ОформлениеКругЖелтый;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ОтображениеПодсказки = ОтображениеПодсказки.Всплывающая;
	
	Если ЗначенияИдентичны Тогда
		ТекстПодсказки = СтрШаблон(НСтр("ru = 'Значение настройки корректно (%1)'"), ИмяКнопки);
		УстановитьСтатусКнопки(Кнопка, Команда, Истина, ТекстПодсказки);
	Иначе
		ТекстПодсказки = СтрШаблон(НСтр("ru = 'Значение настройки не совпадает (%1)'"), ИмяКнопки);
		УстановитьСтатусКнопки(Кнопка, Команда, Ложь, ТекстПодсказки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗначениеНастройки(ИмяНастройки, ЗначениеИсходное, ИменаКнопокПоТипуНастроек)
	
	ТипЗначения = ПредопределенныеНастройки.ТипЗначенияПредопределеннойНастройки(ЗначениеИсходное);
	ИмяКнопки = ИменаКнопокПоТипуНастроек[ТипЗначения];
	
	ЗначенияИдентичны = ЗначениеНастройкиВерно(ИмяНастройки, ЗначениеИсходное);
	
	УстановитьОформлениеКнопки(ИмяКнопки, ЗначенияИдентичны);
	
КонецПроцедуры

&НаСервере
Функция ЗначениеНастройкиСоответствуетЗначению(ИмяНастройки, Знач ТипЗначения, Значение)
	Если ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка")
		Или ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево")
		Или ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка") Тогда
		
		ЗначенияИдентичны = Значение = ПредопределенныеНастройки.Значение(ИмяНастройки) ;
		
	ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив") Тогда 
		
		ЗначенияИдентичны = МассивыИдентичны(Значение, ПредопределенныеНастройки.Значение(ИмяНастройки));
		
	ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений")
		Или ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение") Тогда 
		
		ЗначенияИдентичны =  Не КоллекцияРазличаются(Значение, ПредопределенныеНастройки.Значение(ИмяНастройки)); 
		
	КонецЕсли;
	Возврат ЗначенияИдентичны
КонецФункции

&НаСервере
Процедура УстановитьСтатусКнопки(Кнопка, Команда, Статус, ТекстПодсказки)
	
	Если Статус Тогда
		Кнопка.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
	Иначе
		Кнопка.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
	КонецЕсли;
	
	ДобавитьПодсказку(Команда, ТекстПодсказки);
	
КонецПроцедуры

&НаСервере
Функция МассивыИдентичны(ПервыйМассив, ВторойМассив) 
	ЭлементовМассива = ПервыйМассив.Количество();
	МассивыСовпадают = (ЭлементовМассива = ВторойМассив.Количество());
	Если МассивыСовпадают Тогда
		Для Сч = 1 По ЭлементовМассива Цикл
			Если ПервыйМассив[Сч - 1] <> ВторойМассив[Сч - 1] Тогда
				Возврат Ложь
			КонецЕсли
			;
		КонецЦикла;
	КонецЕсли;
	Возврат МассивыСовпадают
КонецФункции

&НаСервере
Функция КоллекцияРазличаются(ТЗ1,ТЗ2,ИсключаемыеКолонки="")

    ИсклКол = СтрРазделить(ИсключаемыеКолонки, ",", Ложь);

    Если ТЗ1.Количество()<>ТЗ2.Количество() Тогда
        Если ТЗ1.Количество()=1 И ТЗ2.Количество()=0 Тогда
            Для К=0 По ТЗ1.Количество()-1 Цикл
                Стр1=ТЗ1[К];
                Для М=0 По ТЗ1.Колонки.Количество()-1 Цикл
                    Имя=ТЗ1.Колонки[М].Имя;
                    Если ИсклКол.Найти(Имя)<>Неопределено Тогда
                        Продолжить;
                    КонецЕсли;
                    Если ЗначениеЗаполнено(Стр1[Имя]) Тогда
                        Возврат Истина;
                    КонецЕсли;
                КонецЦикла;
            КонецЦикла;
            Возврат Ложь;
        ИначеЕсли ТЗ1.Количество()=0 И ТЗ2.Количество()=1 Тогда
            Для К=0 По ТЗ2.Количество()-1 Цикл
                Стр2=ТЗ2[К];
                Для М=0 По ТЗ1.Колонки.Количество()-1 Цикл
                    Имя=ТЗ1.Колонки[М].Имя;
                    Если ИсклКол.Найти(Имя)<>Неопределено Тогда
                        Продолжить;
                    КонецЕсли;
                    Если ЗначениеЗаполнено(Стр2[Имя]) Тогда
                        Возврат Истина;
                    КонецЕсли;
                КонецЦикла;
            КонецЦикла;
            Возврат Ложь;
        Иначе
            Возврат Истина;
        КонецЕсли;
    КонецЕсли;
    Для К=0 По ТЗ1.Количество()-1 Цикл
        Стр1=ТЗ1[К];
        Стр2=ТЗ2[К];
        Для М=0 По ТЗ1.Колонки.Количество()-1 Цикл
            Имя=ТЗ1.Колонки[М].Имя;
            Если ИсклКол.Найти(Имя)<>Неопределено Тогда
                Продолжить;
            КонецЕсли;
            Если Стр1[Имя]<>Стр2[Имя] Тогда
                Возврат Истина;
            КонецЕсли;
        КонецЦикла;
    КонецЦикла;
    Возврат Ложь;
КонецФункции

&НаСервере
Процедура ДобавитьПодсказку(Команда, ТекстЭтапа)
	
	Подсказка = Команда.Подсказка;
	Если ПустаяСтрока(Команда.Подсказка) Тогда
		Команда.Подсказка = ТекстЭтапа;
	Иначе
		Команда.Подсказка = СтрШаблон(НСтр("ru = '%1
			|%2'"), Подсказка, ТекстЭтапа);	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ИменаКнопокСозданияПоТипамЗначений()

	ИменаКнопокПоТипуНастроек = Новый Соответствие;
	
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка"), "СозданиеПредопределеннойНастройкиСтрока");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево"), "СозданиеПредопределеннойНастройкиБулево");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка"), "СозданиеПредопределеннойНастройкиСсылка");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив"), "СозданиеПредопределеннойНастройкиМассив");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение"), "СозданиеПредопределеннойНастройкиКлючЗначение");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений"), "СозданиеПредопределеннойНастройкиТаблицаЗначений");
	Возврат ИменаКнопокПоТипуНастроек
	
КонецФункции

&НаСервере
Функция ИменаКнопокУдаленияПоТипамЗначений()

	ИменаКнопокПоТипуНастроек = Новый Соответствие;
	
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка"), "УдалениеПредопределеннойНастройкиСтрока");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево"), "УдалениеПредопределеннойНастройкиБулево");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка"), "УдалениеПредопределеннойНастройкиСсылка");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив"), "УдалениеПредопределеннойНастройкиМассив");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение"), "УдалениеПредопределеннойНастройкиКлючЗначение");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений"), "УдалениеПредопределеннойНастройкиТаблицаЗначений");
	Возврат ИменаКнопокПоТипуНастроек
	
КонецФункции

&НаСервере
Функция ИменаКнопокУстановкиПоТипамЗначений()

	ИменаКнопокПоТипуНастроек = Новый Соответствие;
	
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка"), "УстановитьЗначениеПредопределеннойНастройкиСтрока");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево"), "УстановитьЗначениеПредопределеннойНастройкиБулево");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка"), "УстановитьЗначениеПредопределеннойНастройкиСсылка");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив"), "УстановитьЗначениеПредопределеннойНастройкиМассив");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение"), "УстановитьЗначениеПредопределеннойНастройкиКлючЗначение");
	ИменаКнопокПоТипуНастроек.Вставить(ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений"), "УстановитьЗначениеПредопределеннойНастройкиТаблицаЗначений");
	Возврат ИменаКнопокПоТипуНастроек
	
КонецФункции

&НаСервере
Процедура ДобавитьТестовуюСтроку(ТаблицаНастроек)

	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор = Новый УникальныйИдентификатор("98048614-6212-466a-af5c-a08e5ca56056");
	НоваяНастройка.ИмяНастройки = ИмяНастройкиТестовойСтрки();
	НоваяНастройка.Наименование = "Насройка проверки строкового значения";
	НоваяНастройка.Описание = НСтр("ru ='Проврерка тескстовой настройки.Данная настройка создаетсяся для проверки правильности работы механизма предопределенных настроек.'");
	НоваяНастройка.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка");
	НоваяНастройка.Значение = НСтр("ru ='Ваше будущее - за 1С!'");

КонецПроцедуры

&НаСервере
Процедура ДобавитьТестовоеБулево(ТаблицаНастроек)

	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор = Новый УникальныйИдентификатор("bfc480f2-023e-40d3-ab52-fecb853edf3c");
	НоваяНастройка.ИмяНастройки = ИмяНастройкиТестовогоБулево();
	НоваяНастройка.Хранилище = Неопределено;
	НоваяНастройка.Наименование = НСтр("ru ='Кошки любят виноград?'");
	НоваяНастройка.Описание = НСтр("ru ='Проврерка булевой настройки. Данная настройка создаетсяся для проверки правильности работы механизма предопределенных настроек.'");
	НоваяНастройка.Значение = Истина;
	НоваяНастройка.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Булево");

КонецПроцедуры

&НаСервере
Процедура ИзменитьТестовоеБулево(ТаблицаНастроек)
	
	СуществующаяНастройка = ТаблицаНастроек.Найти(ИмяНастройкиТестовогоБулево(), "ИмяНастройки");
	Если СуществующаяНастройка <> Неопределено Тогда
		СуществующаяНастройка.Значение = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТестовуюСтроку(ТаблицаНастроек)
	
	СуществующаяНастройка = ТаблицаНастроек.Найти(ИмяНастройкиТестовойСтрки(), "ИмяНастройки");
	Если СуществующаяНастройка <> Неопределено Тогда
		СуществующаяНастройка.Значение = СуществующаяНастройка.Значение + " - 1";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТестовуюСсылку(ТаблицаНастроек)
	
	СуществующаяНастройка = ТаблицаНастроек.Найти(ИмяНастройкиТестовойСсылки(), "ИмяНастройки");
	Если СуществующаяНастройка <> Неопределено Тогда
		СуществующаяНастройка.Значение = Справочники.ПредопределенныеНастройки.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТестовыйМассив(ТаблицаНастроек)
	
	МассивПример = Новый Массив;
	МассивПример.Добавить(5);
	МассивПример.Добавить("Семь");
	МассивПример.Добавить(3.14);

	СуществующаяНастройка = ТаблицаНастроек.Найти(ИмяНастройкиТестовыйМассив(), "ИмяНастройки");
	Если СуществующаяНастройка <> Неопределено Тогда
		СуществующаяНастройка.Значение = МассивПример;
		СуществующаяНастройка.Хранилище = Новый ХранилищеЗначения(МассивПример);;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТестовыйКлючЗначение(ТаблицаНастроек)
	
	ТаблицаЗначенийПример = Новый ТаблицаЗначений;
	ТаблицаЗначенийПример.Колонки.Добавить("Ключ", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийПример.Колонки.Добавить("Значение",);
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ1";
	Строка.Значение = "Значение1";
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ2";
	Строка.Значение = "Значение2";
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ3";
	Строка.Значение = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");	
	
	СуществующаяНастройка = ТаблицаНастроек.Найти(ИмяНастройкиТестовыйКлючЗначение(), "ИмяНастройки");
	Если СуществующаяНастройка <> Неопределено Тогда
		СуществующаяНастройка.Значение = ТаблицаЗначенийПример;
		СуществующаяНастройка.Хранилище = Новый ХранилищеЗначения(ТаблицаЗначенийПример);;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТестовуюТаблицуЗначений(ТаблицаНастроек)
	
	ТаблицаЗначенийПример = Новый ТаблицаЗначений;
	ТаблицаЗначенийПример.Колонки.Добавить("Колонка1", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийПример.Колонки.Добавить("Колонка2",);
	ТаблицаЗначенийПример.Колонки.Добавить("Колонка3",);
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Колонка1 = "Ключ1";
	Строка.Колонка2 = "Значение1";
	Строка.Колонка3 = "Значение1";
	
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Колонка1 = "Ключ2";
	Строка.Колонка2 = "Значение2";
	Строка.Колонка3 = "Значение3";
	
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Колонка1 = "Ключ3";
	Строка.Колонка2 = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений");	
	Строка.Колонка3 = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Строка");	

	СуществующаяНастройка = ТаблицаНастроек.Найти(ИмяНастройкиТаблицаЗначений(), "ИмяНастройки");
	Если СуществующаяНастройка <> Неопределено Тогда
		СуществующаяНастройка.Значение = ТаблицаЗначенийПример;
		СуществующаяНастройка.Хранилище = Новый ХранилищеЗначения(ТаблицаЗначенийПример);;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ДобавитьТестовуюСсылку(ТаблицаНастроек)

	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор = Новый УникальныйИдентификатор("afc120f2-645d-12d3-ab52-faab754edf3c");
	НоваяНастройка.ИмяНастройки = ИмяНастройкиТестовойСсылки();
	НоваяНастройка.Хранилище = Неопределено;
	НоваяНастройка.Наименование = НСтр("ru ='Настройка текущего релиза подсистемы'");
	НоваяНастройка.Описание = НСтр("ru ='Проврерка ссылочной настройки. Данная настройка создаетсяся для проверки правильности работы механизма предопределенных настроек.'");
	НоваяНастройка.Значение = ПредопределенныеНастройки.НастройкаПоИмени("ВерсияПодсистемы");
	НоваяНастройка.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Ссылка");

КонецПроцедуры

&НаСервере
Процедура ДобавитьТестовуюТаблицуЗначений(ТаблицаНастроек)

	ТаблицаЗначенийПример = Новый ТаблицаЗначений;
	ТаблицаЗначенийПример.Колонки.Добавить("Столбец1", Новый ОписаниеТипов("Число"));
	ТаблицаЗначенийПример.Колонки.Добавить("Столбец2", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийПример.Колонки.Добавить("Столбец3");
	Строка = ТаблицаЗначенийПример.Добавить();
	
	Строка.Столбец1 = 1;
	Строка.Столбец2 = "Значение1";
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Столбец1 = 2;
	Строка.Столбец2 = "Значение2";
	Строка.Столбец3 = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений");
	

	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор = Новый УникальныйИдентификатор("7f988333-8c73-46a8-14a6-455a55a15bbe");
	НоваяНастройка.ИмяНастройки = ИмяНастройкиТаблицаЗначений();
	НоваяНастройка.Хранилище = Новый ХранилищеЗначения(ТаблицаЗначенийПример);
	НоваяНастройка.Наименование = "Таблица значений тестовых настроек";
	НоваяНастройка.Значение = ТаблицаЗначенийПример;
	НоваяНастройка.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.ТаблицаЗначений");
	НоваяНастройка.Описание = НСтр("ru ='Проврерка настройки с данными таблицы значений. Данная настройка создаетсяся для проверки правильности работы механизма предопределенных настроек.'");

КонецПроцедуры

&НаСервере
Процедура ДобавитьТестовыйКлючЗначение(ТаблицаНастроек)

	ТаблицаЗначенийПример = Новый ТаблицаЗначений;
	ТаблицаЗначенийПример.Колонки.Добавить("Ключ", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийПример.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ1";
	Строка.Значение = "Значение1";
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ2";
	Строка.Значение = "Значение2";

	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор = Новый УникальныйИдентификатор("2f111348-2c73-32a8-12a6-121a40a88bbe");
	НоваяНастройка.ИмяНастройки = ИмяНастройкиТестовыйКлючЗначение();
	НоваяНастройка.Хранилище = Новый ХранилищеЗначения(ТаблицаЗначенийПример);
	НоваяНастройка.Наименование = "Таблица значений ключ значение - тестовые ";
	НоваяНастройка.Значение = ТаблицаЗначенийПример;
	НоваяНастройка.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");
	НоваяНастройка.Описание = НСтр("ru ='Проврерка настройки с данными соответствия. Данная настройка создаетсяся для проверки правильности работы механизма предопределенных настроек.'");

КонецПроцедуры

&НаСервере
Процедура ДобавитьТестовыйМассив(ТаблицаНастроек)

	МассивПример = Новый Массив;
	МассивПример.Добавить(1);
	МассивПример.Добавить("Два");
	МассивПример.Добавить(3.14);

	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор = Новый УникальныйИдентификатор("6f205348-2c73-46a8-98a6-851a40a12bbe");
	НоваяНастройка.ИмяНастройки = ИмяНастройкиТестовыйМассив();
	НоваяНастройка.Хранилище = Новый ХранилищеЗначения(МассивПример);
	НоваяНастройка.Значение = МассивПример;
	НоваяНастройка.Наименование = "Массив тестовых настроек";
	НоваяНастройка.ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.Массив");
КонецПроцедуры


// Процедура удаляет предопределенные настройки с сервера.
&НаСервере
Процедура УдалениеПредопределенныхНастроекНаСервере()

	ОбновитьПовторноИспользуемыеЗначения();
	ТаблицаНастроек = ТаблицаПредопределенныхТестовыхНастроек();

	Для Каждого Строка Из ТаблицаНастроек Цикл
		Настройка = ПредопределенныеНастройкиПовтИсп.СсылкаПредопределеннойНастройки(Строка.ИмяНастройки);

		Если ЗначениеЗаполнено(Настройка) И ТипЗнч(Настройка) = Тип("СправочникСсылка.ПредопределенныеНастройки") Тогда
			ОбъектНастройки = Настройка.ПолучитьОбъект();
			ОбъектНастройки.Удалить();
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Процедура проверяет, были ли удалены предопределенные настройки.
&НаСервере
Процедура ПроверитьУдаленныеНастройки()

	НастройкиУдалены = Истина;
	ТаблицаНастроек = ТаблицаПредопределенныхТестовыхНастроек();

	Для Каждого Строка Из ТаблицаНастроек Цикл
		
		ИмяНастройки = Строка.ИмяНастройки;
		ТипЗначения = Строка.ТипЗначения;
		
		НастройкаУдалена = НастройкаУдалена(ИмяНастройки, ТипЗначения);
		ОбновитьИнформациюНаФормеНастройкаУдалена(ИмяНастройки, ТипЗначения, НастройкаУдалена);

		Если Не НастройкаУдалена Тогда
			НастройкиУдалены = Ложь;
		КонецЕсли;

	КонецЦикла;

	Если НастройкиУдалены Тогда
		Элементы.НастройкиУдалены.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
		Элементы.НастройкиУдалены.Подсказка = НСтр("ru ='Тестовые настройки не обнаружены'");
	Иначе
		Элементы.НастройкиУдалены.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
		Элементы.НастройкиУдалены.Подсказка = НСтр("ru ='Тестовые настройки найдены в базе'");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюНаФормеНастройкаУдалена(ИмяНастройки, ТипЗначения, НастройкаУдалена)
	
	ИменаКнопок = ИменаКнопокУдаленияПоТипамЗначений();
	ИмяКнопки = ИменаКнопок[ТипЗначения];
	Кнопка = Элементы[ИмяКнопки];
	Команда = Команды[ИмяКнопки];
	
	Если НастройкаУдалена Тогда
		Кнопка.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
		ДобавитьПодсказку(Команда, СтрШаблон(НСтр("ru = 'Удаление настройки %1 корректно'"), ИмяНастройки));
	Иначе
		Кнопка.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
		ДобавитьПодсказку(Команда, НСтр("ru = 'Настройка не удалена'"));
	КонецЕсли;
	
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ОтображениеПодсказки = ОтображениеПодсказки.Всплывающая;
	
КонецПроцедуры

&НаСервере
Функция НастройкаУдалена(ИмяНастройки, ТипЗначения)
	Настройка = ПредопределенныеНастройки.Значение(ИмяНастройки);
	НастройкаУдалена = Настройка = ПредопределенноеЗначение("Справочник.ПредопределенныеНастройки.ПустаяСсылка");
	Возврат НастройкаУдалена;
КонецФункции

&НаСервере
Процедура УдалитьНастройкуИзКоманды(ИмяНастройки)
	
	ПредопределеннаяНастройка = ПредопределенныеНастройки.НастройкаПоИмени(ИмяНастройки);
	ОбъектНастройки = ПредопределеннаяНастройка.ПолучитьОбъект();
	ТипЗначения = ОбъектНастройки.ТипЗначения;
	ОбъектНастройки.Удалить();

	ПредопределеннаяНастройка = ПредопределенныеНастройки.НастройкаПоИмени(ИмяНастройки);
	ЗначениеПредопределеннойНастройки = ПредопределенныеНастройки.Значение(ИмяНастройки, Ложь);
	
	НастройкаУдалена = НастройкаУдалена(ИмяНастройки, ТипЗначения);
	ОбновитьИнформациюНаФормеНастройкаУдалена(ИмяНастройки, ТипЗначения, НастройкаУдалена);
	
КонецПроцедуры

&НаСервере
Функция ИмяНастройкиТестовойСтрки()
	Возврат "ТестированиеСтроковогоЗначения";
КонецФункции

&НаСервере
Функция ИмяНастройкиТестовогоБулево()
	Возврат "ТестБулево";
КонецФункции

&НаСервере
Функция ИмяНастройкиТестовойСсылки()
	Возврат "ТестСсылки";
КонецФункции

&НаСервере
Функция ИмяНастройкиТестовыйМассив()
	Возврат "ТестМассива";
КонецФункции

&НаСервере
Функция ИмяНастройкиТестовыйКлючЗначение()
	Возврат "ТестКлючЗначение";
КонецФункции

&НаСервере
Функция ИмяНастройкиТаблицаЗначений()
	Возврат "ТестТаблицаЗначений";
КонецФункции

&НаСервере
Процедура СозданиеПредопределеннойНастройкиСтрокаНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовуюСтроку(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);
	
	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовойСтрки());
	
	ПроверитьЗначениеНастройки(ИмяНастройкиТестовойСтрки(), ТаблицаНастроек[0].Значение, ИменаКнопокСозданияПоТипамЗначений());
	
КонецПроцедуры

&НаСервере
Процедура СозданиеПредопределеннойНастройкиБулевоНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовоеБулево(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовогоБулево());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовогоБулево(), ТаблицаНастроек[0].Значение, ИменаКнопокСозданияПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура СозданиеПредопределеннойНастройкиСсылкаНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовуюСсылку(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовойСсылки());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовойСсылки(), ТаблицаНастроек[0].Значение, ИменаКнопокСозданияПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура СозданиеПредопределеннойНастройкиМассивНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовыйМассив(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовыйМассив());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовыйМассив(), ТаблицаНастроек[0].Значение, ИменаКнопокСозданияПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура СозданиеПредопределеннойНастройкиКлючЗначениеНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовыйКлючЗначение(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовыйКлючЗначение());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовыйКлючЗначение(), ТаблицаНастроек[0].Значение, ИменаКнопокСозданияПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура СозданиеПредопределеннойНастройкиТаблицаЗначенийНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовуюТаблицуЗначений(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТаблицаЗначений());

	ПроверитьЗначениеНастройки(ИмяНастройкиТаблицаЗначений(), ТаблицаНастроек[0].Значение, ИменаКнопокСозданияПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПредопределеннойНастройкиСтрокаНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовуюСтроку(ТаблицаНастроек);
	ИзменитьТестовуюСтроку(ТаблицаНастроек);
	
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);
	
	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовойСтрки());
	
	ПроверитьЗначениеНастройки(ИмяНастройкиТестовойСтрки(), ТаблицаНастроек[0].Значение, ИменаКнопокУстановкиПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПредопределеннойНастройкиБулевоНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовоеБулево(ТаблицаНастроек);
	ИзменитьТестовоеБулево(ТаблицаНастроек);
	
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовогоБулево());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовогоБулево(), ТаблицаНастроек[0].Значение, ИменаКнопокУстановкиПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПредопределеннойНастройкиСсылкаНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовуюСсылку(ТаблицаНастроек);
	ИзменитьТестовуюСсылку(ТаблицаНастроек);
	
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовойСсылки());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовойСсылки(), ТаблицаНастроек[0].Значение, ИменаКнопокУстановкиПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПредопределеннойНастройкиМассивНаСервере()

	МассивПример = Новый Массив;
	МассивПример.Добавить(5);
	МассивПример.Добавить("Семь");
	МассивПример.Добавить(3.14);

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовыйМассив(ТаблицаНастроек);
	ТаблицаНастроек[0].Значение = МассивПример;
	ТаблицаНастроек[0].Хранилище = Новый ХранилищеЗначения(МассивПример);
	
	ИзменитьТестовыйМассив(ТаблицаНастроек);
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовыйМассив());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовыйМассив(), ТаблицаНастроек[0].Значение, ИменаКнопокУстановкиПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПредопределеннойНастройкиКлючЗначениеНаСервере()

	ТаблицаЗначенийПример = Новый ТаблицаЗначений;
	ТаблицаЗначенийПример.Колонки.Добавить("Ключ", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийПример.Колонки.Добавить("Значение",);
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ1";
	Строка.Значение = "Значение1";
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ2";
	Строка.Значение = "Значение2";
	Строка = ТаблицаЗначенийПример.Добавить();
	Строка.Ключ = "Ключ3";
	Строка.Значение = ПредопределенноеЗначение("Перечисление.ТипыЗначенияПредопределенныхЗначений.КлючЗначение");	

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовыйКлючЗначение(ТаблицаНастроек);
	ИзменитьТестовыйКлючЗначение(ТаблицаНастроек);
	ТаблицаНастроек[0].Значение = ТаблицаЗначенийПример;
	ТаблицаНастроек[0].Хранилище = Новый ХранилищеЗначения(ТаблицаЗначенийПример);
	
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТестовыйКлючЗначение());

	ПроверитьЗначениеНастройки(ИмяНастройкиТестовыйКлючЗначение(), ТаблицаНастроек[0].Значение, ИменаКнопокУстановкиПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УдалениеПредопределеннойНастройкиСтрокаНаСервере()

	УдалитьНастройкуИзКоманды(ИмяНастройкиТестовойСтрки());

КонецПроцедуры

&НаСервере
Процедура УдалениеПредопределеннойНастройкиБулевоНаСервере()

	УдалитьНастройкуИзКоманды(ИмяНастройкиТестовогоБулево());

КонецПроцедуры

&НаСервере
Процедура УдалениеПредопределеннойНастройкиСсылкаНаСервере()

	УдалитьНастройкуИзКоманды(ИмяНастройкиТестовойСсылки());

КонецПроцедуры

&НаСервере
Процедура УдалениеПредопределеннойНастройкиМассивНаСервере()

	УдалитьНастройкуИзКоманды(ИмяНастройкиТестовыйМассив());

КонецПроцедуры

&НаСервере
Процедура УдалениеПредопределеннойНастройкиКлючЗначениеНаСервере()

	УдалитьНастройкуИзКоманды(ИмяНастройкиТестовыйКлючЗначение());

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПредопределеннойНастройкиТаблицаЗначенийНаСервере()

	ТаблицаНастроек = ПредопределенныеНастройки.НоваяТаблицаПредопределенныхНастроек();
	ДобавитьТестовуюТаблицуЗначений(ТаблицаНастроек);
	ИзменитьТестовуюТаблицуЗначений(ТаблицаНастроек);
	
	ПредопределенныеНастройки.ОбновитьНастройкиВБазе(ТаблицаНастроек);

	ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройкиТаблицаЗначений());

	ПроверитьЗначениеНастройки(ИмяНастройкиТаблицаЗначений(), ТаблицаНастроек[0].Значение, ИменаКнопокУстановкиПоТипамЗначений());

КонецПроцедуры

&НаСервере
Процедура УдалениеПредопределеннойНастройкиТаблицаЗначенийНаСервере()
	
	УдалитьНастройкуИзКоманды(ИмяНастройкиТаблицаЗначений());

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеНаФормеПоТекущейНастройке(ИмяНастройки)
	ПредопределеннаяНастройка = ПредопределенныеНастройки.НастройкаПоИмени(ИмяНастройки);
	ЗначениеПредопределеннойНастройки = ПредопределенныеНастройки.Значение(ИмяНастройки, Истина);
КонецПроцедуры

#КонецОбласти